{-# OPTIONS --cubical --rewriting #-}
open import Primitives public
--open import Agda.Primitive public

module Church.InitialAlgebras where

open import TypeSystem public
open import Graph.Target public

--{-# BUILTIN REWRITE _â‰¡_ #-}

--In this file, we prove the existence of initial algebras for functors (module InitialOfFunctor)
--and indexed functors (module InitialOfIndexedFunctor).
--The Church encodings depend pointwise on the algebra structure.

--------------------------------------------------

{-This module postulates its parameters, rather than getting parameters, because we need to add a rewrite rule.
  In order to make sure that modalities are enforced correctly, we add dummies with modalities id and #.
-}
module InitialOfFunctor (idDummy :{id} Set) (#Dummy :{#} Set) where

  postulate
    F :{#} âˆ€{â„“} â†’ Set â„“ â†’ Set â„“
    F' :{Â¶} âˆ€{â„“A â„“B} {A :{#} Set â„“A} {B :{#} Set â„“B} (f : A â†’ B) â†’ (F A â†’ F B)
    rw-F-id : âˆ€{â„“} {A :{#} Set â„“} â†’ F' (id{â„“}{A}) â‰¡ id{â„“}{F A}
    rw-F-hom : âˆ€{â„“A â„“B â„“C} {A :{#} Set â„“A} {B :{#} Set â„“B} {C :{#} Set â„“C} {f : A â†’ B} {g : B â†’ C} (fa :{#} F A)
         â†’ F' g (F' f fa) â‰¡ F'(g âˆ˜ f) fa

  {-# REWRITE rw-F-id #-}
  {-# REWRITE rw-F-hom #-}

  IsAlg :{#} âˆ€{â„“} â†’ (A : Set â„“) â†’ Set â„“
  IsAlg A = F A â†’ A

  IsMph :{#} âˆ€{â„“A â„“B} {A : Set â„“A} {B : Set â„“B} (mkA : IsAlg A) (mkB : IsAlg B) (f : A â†’ B) â†’ Set (â„“A âŠ” â„“B)
  IsMph {â„“A}{â„“B}{A}{B} mkA mkB f = f âˆ˜ mkA â‰¡ mkB âˆ˜ F' f

  --initial algebra
  Mu :{#} (â„“ : Level) â†’ Set (lsuc â„“)
  Mu â„“ = (X :{#} Set â„“) â†’ (mkX :{Â¶} IsAlg X) â†’ X
  fold : âˆ€{â„“} {X :{#} Set â„“} (mkX :{Â¶} IsAlg X) â†’ (Mu â„“ â†’ X)
  fold {â„“} {X} mkX m = m X mkX
  mkMu : âˆ€{â„“} â†’ IsAlg (Mu â„“)
  mkMu fm X mkX = mkX (F' (fold mkX) fm)
  mfold :{Â¶} âˆ€{â„“} {X :{#} Set â„“}  (mkX :{Â¶} IsAlg X) â†’ IsMph mkMu mkX (fold mkX)
  mfold {â„“} {X} mkX = refl _

  module NaturalityProven {â„“ :{Â¶} Level} where
    postulate
      A B :{#} Set â„“
      mkA :{Â¶} IsAlg A
      mkB :{Â¶} IsAlg B
      f :{Â¶} A â†’ B
      rw-mf : âˆ€{fa : F A} â†’ f (mkA fa) â‰¡ mkB (F' f fa)
    {-# REWRITE rw-mf #-}
    mf :{Â¶} IsMph mkA mkB f
    mf = refl _

    /f/ :{#} (i : ğ•€) â†’ Set â„“
    /f/ = / f /
    mk/f/ :{Â¶} {i :{#} ğ•€} â†’ F (/f/ i) â†’ /f/ i
    mk/f/ {i} fq = glue
      {Ï† = (i â‰£ i0) âˆ¨ (i â‰£ i1)}
      (Î» { ((i â‰£ i0) = pâŠ¤) â†’ mkA fq
         ; ((i â‰£ i1) = pâŠ¤) â†’ mkB fq
         })
      (mkB (F' (pull f i) fq))

    mpull :{Â¶} (i :{#} ğ•€) â†’ IsMph (mk/f/ {i}) mkB (pull f i)
    mpull i = refl _

    naturality-path : (i :{#} ğ•€) â†’ Mu â„“ â†’ B
    naturality-path i = pull f i âˆ˜ fold (mk/f/ {i})

    naturality :{Â¶} f âˆ˜ fold mkA â‰¡ fold mkB
    naturality = path-to-eq naturality-path

  module Naturality {â„“ : Level}
      {A B :{#} Set â„“}
      (mkA :{Â¶} IsAlg A)
      (mkB :{Â¶} IsAlg B)
      (f :{Â¶} A â†’ B)
      (rw-mf : âˆ€{fa : F A} â†’ f (mkA fa) â‰¡ mkB (F' f fa)) -- to be instantiated with refl
    where
    postulate
      naturality :{Â¶} f âˆ˜ fold mkA â‰¡ fold mkB

  downfold : âˆ€{â„“} â†’ Mu (lsuc â„“) â†’ Mu â„“
  downfold = fold mkMu
  mdownfold :{Â¶} âˆ€{â„“} â†’ IsMph mkMu mkMu (downfold {â„“})
  mdownfold = mfold mkMu

  mkLift : âˆ€{â„“} {A :{#} Set â„“} â†’ IsAlg A â†’ IsAlg (Lift A)
  mkLift {â„“} {A} mkA = lift âˆ˜ mkA âˆ˜ F' lower

  mlift :{Â¶} âˆ€{â„“} {A :{#} Set â„“} (mkA :{#} IsAlg A) â†’ IsMph mkA (mkLift mkA) lift
  mlift mkA = refl _

  mlower :{Â¶} âˆ€{â„“} {A :{#} Set â„“} (mkA :{#} IsAlg A) â†’ IsMph (mkLift mkA) mkA lower
  mlower mkA = refl _

  module LoweringLemma where
    module Core {â„“ :{Â¶} Level} {X :{#} Set â„“} (mkX :{Â¶} IsAlg X) where
      liftâˆ˜fold : Mu â„“ â†’ Lift X
      liftâˆ˜fold = lift âˆ˜ fold mkX

      open Naturality {lsuc â„“} mkMu (mkLift mkX) liftâˆ˜fold (refl _)

      naturality' :{Â¶} (Î» (m : Mu (lsuc â„“)) â†’ lift (m (Mu â„“) mkMu X mkX)) â‰¡
               (Î» (m : Mu (lsuc â„“)) â†’ m (Lift X) (mkLift mkX))
      naturality' = naturality
    open Core

    --Note that Lift is the coercion into the next universe,
    --while lift and mkLift are the identity in ParamDTT
    lowerMu : âˆ€{â„“} â†’ Mu (lsuc â„“) â†’ Mu â„“
    lowerMu m X mkX = lower (m (Lift X) (mkLift mkX))
    mlowerMu :{Â¶} âˆ€{â„“} â†’ IsMph mkMu mkMu (lowerMu {â„“})
    mlowerMu = refl _

    lowering-lemma :{Â¶} âˆ€{â„“} â†’ fold mkMu â‰¡ (lowerMu {â„“})
    lowering-lemma = funext (Î» m â†’ #funext Î» X â†’ Â¶funext Î» mkX â†’ cong lower (cong-app (naturality' mkX) m))
  open LoweringLemma using (lowerMu ; mlowerMu ; lowering-lemma)

  module InitialityProven {â„“ :{Â¶} Level} where
    postulate
      B :{#} Set (lsuc â„“)
      mkB :{Â¶} F B â†’ B
      f :{Â¶} Mu â„“ â†’ B
      rw-mf : âˆ€{fm : F (Mu â„“)} â†’ f (mkMu fm) â‰¡ mkB (F' f fm)
    {-# REWRITE rw-mf #-}
    mf :{Â¶} IsMph mkMu mkB f
    mf = refl _

    open Naturality {lsuc â„“} mkMu mkB f (refl _)
    naturality' :{Â¶} f âˆ˜ fold mkMu â‰¡ fold mkB
    naturality' = naturality

    initiality :{Â¶} f âˆ˜ lowerMu â‰¡ fold mkB
    initiality = J (lowering-lemma) (Î» lowerMu' _ â†’ f âˆ˜ lowerMu' â‰¡ fold mkB) naturality'


-------------------------------------------------------------

{-This module postulates its parameters, rather than getting parameters, because we need to add a rewrite rule.
  In order to make sure that modalities are enforced correctly, we add dummies with modalities id and #.
-}
module InitialOfIndexedFunctor {â„“Z :{Â¶} Level} (Z :{#} Set â„“Z) (idDummy :{id} Set) (#Dummy :{#} Set) where

  ZSet :{#} (â„“ : Level) â†’ Set (lsuc â„“ âŠ” â„“Z)
  ZSet â„“ = Z â†’ Set â„“

  _â‡’_ :{#} âˆ€{â„“A â„“B} (A : ZSet â„“A) (B : ZSet â„“B) â†’ Set (â„“B âŠ” â„“A âŠ” â„“Z)
  A â‡’ B = (z :{#} Z) â†’ A z â†’ B z

  z-id :{#} âˆ€{â„“} {A : ZSet â„“} â†’ A â‡’ A
  z-id i = id
  _âŠš_ :{#} âˆ€{â„“A â„“B â„“C} â†’ {A :{#} ZSet â„“A} {B :{#} ZSet â„“B} {C :{#} ZSet â„“C} (g : B â‡’ C) (f : A â‡’ B) â†’ A â‡’ C
  (g âŠš f) = Î» z â†’ g z âˆ˜ f z

  infixr 9 _âŠš_

  postulate
    F :{#} âˆ€{â„“} â†’ (ZSet â„“) â†’ (ZSet â„“)
    F' :{Â¶} âˆ€{â„“A â„“B} {A :{#} ZSet â„“A} {B :{#} ZSet â„“B} â†’ (A â‡’ B) â†’ (F A â‡’ F B)
    rw-F-id : âˆ€{â„“} {A : ZSet â„“} â†’ F' (z-id{â„“}{A}) â‰¡ z-id
    rw-F-hom : âˆ€{â„“A â„“B â„“C} {A : ZSet â„“A} {B : ZSet â„“B} {C : ZSet â„“C}
           {f : A â‡’ B} {g : B â‡’ C}
           (z : Z) â†’ (fa : F A z) â†’ F' g z (F' f z fa) â‰¡ F' (Î» v â†’ g v âˆ˜ f v) z fa

  {-# REWRITE rw-F-id #-}
  {-# REWRITE rw-F-hom #-}

  IsAlg :{#} âˆ€{â„“} â†’ (A : ZSet â„“) â†’ Set (â„“ âŠ” â„“Z)
  IsAlg A = F A â‡’ A

  IsMph :{#} âˆ€{â„“A â„“B} {A : ZSet â„“A} {B : ZSet â„“B} (mkA : IsAlg A) (mkB : IsAlg B) (f : A â‡’ B) â†’ Set (â„“A âŠ” â„“B âŠ” â„“Z)
  IsMph {â„“A}{â„“B}{A}{B} mkA mkB f = f âŠš mkA â‰¡ mkB âŠš F' f

  --initial algebra
  Mu :{#} (â„“ : Level) â†’ ZSet (lsuc â„“ âŠ” â„“Z)
  Mu â„“ z = (X :{#} ZSet â„“) â†’ (mkX :{Â¶} IsAlg X) â†’ X z
  fold : âˆ€{â„“} {X :{#} ZSet â„“} (mkX :{Â¶} IsAlg X) â†’ (Mu â„“ â‡’ X)
  fold {â„“} {X} mkX z m = m X mkX
  mkMu : âˆ€{â„“} â†’ IsAlg (Mu â„“)
  mkMu z fm X mkX = mkX z (F' (fold mkX) z fm)
  mfold :{Â¶} âˆ€{â„“} {X :{#} ZSet â„“} (mkX :{Â¶} IsAlg X) â†’ IsMph mkMu mkX (fold mkX)
  mfold {â„“} {X} mkX = refl _

  Z/_/ :{#} âˆ€{â„“} {A B : ZSet â„“} (f :{Â¶} A â‡’ B) â†’ ğ•€ â†’ ZSet â„“
  Z/ f / i z = / f z / i
  zpush : âˆ€{â„“} {A B :{#} ZSet â„“} (f :{Â¶} A â‡’ B) â†’ (i :{#} ğ•€) â†’ A â‡’ Z/ f / i
  zpush f i z a = push (f z) i a
  zpull : âˆ€{â„“} {A B :{#} ZSet â„“} (f :{Â¶} A â‡’ B) â†’ (i :{#} ğ•€) â†’ Z/ f / i â‡’ B
  zpull f i z q = pull (f z) i q

  module NaturalityProven {â„“ :{Â¶} Level} where
    postulate
      A B :{#} ZSet â„“
      mkA :{Â¶} IsAlg A
      mkB :{Â¶} IsAlg B
      f :{Â¶} A â‡’ B
      rw-mf : âˆ€{z} {fa : F A z} â†’ f z (mkA z fa) â‰¡ mkB z (F' f z fa)
    {-# REWRITE rw-mf #-}
    mf :{Â¶} IsMph mkA mkB f
    mf = refl _
    /f/ :{#} (i : ğ•€) â†’ ZSet â„“
    /f/ = Z/ f /
    mk/f/ :{Â¶} {i :{#} ğ•€} â†’ F (/f/ i) â‡’ /f/ i
    mk/f/ {i} z fq = glue
      {Ï† = (i â‰£ i0) âˆ¨ (i â‰£ i1)}
      (Î» { ((i â‰£ i0) = pâŠ¤) â†’ mkA z fq
         ; ((i â‰£ i1) = pâŠ¤) â†’ mkB z fq
         })
      (mkB z (F' (zpull f i) z fq))

    mzpull :{Â¶} (i :{#} ğ•€) â†’ IsMph (mk/f/ {i}) mkB (zpull f i)
    mzpull i = refl _

    naturality-path : (i :{#} ğ•€) â†’ Mu â„“ â‡’ B
    naturality-path i = zpull f i âŠš fold (mk/f/ {i})

    naturality :{Â¶} f âŠš fold mkA â‰¡ fold mkB
    naturality = path-to-eq naturality-path

  module Naturality {â„“ :{Â¶} Level}
      {A B :{#} ZSet â„“}
      (mkA :{Â¶} IsAlg A)
      (mkB :{Â¶} IsAlg B)
      (f :{Â¶} A â‡’ B)
      (rz-mf : âˆ€{z} {fa : F A z} â†’ f z (mkA z fa) â‰¡ mkB z (F' f z fa))
    where
    postulate
      naturality :{Â¶} f âŠš fold mkA â‰¡ fold mkB

  downfold : âˆ€{â„“} â†’ Mu (lsuc (â„“ âŠ” â„“Z)) â‡’ Mu (â„“ âŠ” â„“Z)
  downfold = fold mkMu
  mdownfold :{Â¶} âˆ€{â„“} â†’ IsMph mkMu mkMu (downfold {â„“ âŠ” â„“Z})
  mdownfold = mfold mkMu

  ZLift : âˆ€{â„“} â†’ ZSet â„“ â†’ ZSet (lsuc â„“)
  ZLift A z = Lift (A z)
  zlift : âˆ€{â„“} {A :{#} ZSet â„“} â†’ A â‡’ ZLift A
  zlift z a = lift a
  zlower : âˆ€{â„“} {A :{#} ZSet â„“} â†’ ZLift A â‡’ A
  zlower z a = lower a

  mkLift : âˆ€{â„“} {A :{#} ZSet â„“} â†’ IsAlg A â†’ IsAlg (ZLift A)
  mkLift {â„“} {A} mkA = zlift âŠš mkA âŠš F' zlower

  mlift :{Â¶} âˆ€{â„“} {A :{#} ZSet â„“} (mkA :{#} IsAlg A) â†’ IsMph mkA (mkLift mkA) zlift
  mlift mkA = refl _

  mlower :{Â¶} âˆ€{â„“} {A :{#} ZSet â„“} (mkA :{#} IsAlg A) â†’ IsMph (mkLift mkA) mkA zlower
  mlower mkA = refl _

  module LoweringLemma where
    module Core {â„“ :{Â¶} Level} {X :{#} ZSet (â„“ âŠ” â„“Z)} (mkX :{Â¶} IsAlg X) where
      liftâˆ˜fold : Mu (â„“ âŠ” â„“Z) â‡’ ZLift X
      liftâˆ˜fold = zlift âŠš fold mkX

      open Naturality {lsuc (â„“ âŠ” â„“Z)} mkMu (mkLift mkX) liftâˆ˜fold (refl _)

      naturality' :{Â¶} (Î» z m â†’ lift (m (Mu (â„“ âŠ” â„“Z)) mkMu X mkX))
             â‰¡ (Î» z m â†’ m (ZLift X) (mkLift mkX))
      naturality' = naturality
    open Core

    --Note that Lift is the coercion into the next universe,
    --while lift and mkLift are the identity in ParamDTT
    lowerMu : âˆ€{â„“} â†’ Mu (lsuc â„“) â‡’ Mu â„“
    lowerMu z m X mkX = lower (m (ZLift X) (mkLift mkX))
    mlowerMu :{Â¶} âˆ€{â„“} â†’ IsMph mkMu mkMu (lowerMu {â„“})
    mlowerMu = refl _

    lowering-lemma :{Â¶} {â„“ :{Â¶} Level} â†’ fold mkMu â‰¡ (lowerMu {â„“ âŠ” â„“Z})
    lowering-lemma {â„“} = #funext Î» z â†’ funext Î» m â†’ #funext Î» X â†’ Â¶funext Î» (mkX : IsAlg X) â†’
      cong {lsuc (â„“ âŠ” â„“Z)}{â„“ âŠ” â„“Z}{Lift (X z)}{X z} lower (cong-app (#cong-app (naturality' {â„“} {X} mkX) z) m)
    {-
      J {_}{_}
        {Mu (lsuc (â„“ âŠ” â„“Z)) â‡’ ZLift X}
        {Î» z m â†’ lift (m (Mu (â„“ âŠ” â„“Z)) mkMu X mkX)}
        {Î» z m â†’ m (ZLift X) (mkLift mkX)}
        (naturality' {â„“}{X} mkX)
        (Î» h _ â†’ (m (Mu (â„“ âŠ” â„“Z)) mkMu X mkX) â‰¡ lower (h z m)) (refl _)
    -}

  open LoweringLemma using (lowerMu ; mlowerMu ; lowering-lemma)

  module InitialityProven {â„“ :{Â¶} Level} where
    postulate
      B :{#} ZSet (lsuc (â„“ âŠ” â„“Z))
      mkB :{Â¶} F B â‡’ B
      f :{Â¶} Mu (â„“ âŠ” â„“Z) â‡’ B
      rw-mf : âˆ€{z}{fm : F (Mu _) z} â†’ f z (mkMu z fm) â‰¡ mkB z (F' f z fm)
    {-# REWRITE rw-mf #-}
    mf :{Â¶} IsMph mkMu mkB f
    mf = refl _

    open Naturality {lsuc (â„“ âŠ” â„“Z)} mkMu mkB f (refl _)
    naturality' :{Â¶} f âŠš fold mkMu â‰¡ fold mkB
    naturality' = naturality

    initiality :{Â¶} f âŠš lowerMu â‰¡ fold mkB
    initiality = J (lowering-lemma {â„“ âŠ” â„“Z}) (Î» lowerMu' _ â†’ f âŠš lowerMu' â‰¡ fold mkB) naturality'
